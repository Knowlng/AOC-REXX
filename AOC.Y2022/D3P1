/* REXX */
dsn_in = "DATA.Y22D3"
"ALLOC DD(DSIN) DATASET('"dsn_in"') SHR"
"EXECIO * DISKR DSIN (FINIS STEM input.)"
"FREE FILE(DSIN)"

lower_case = "abcdefghijklmnopqrstuvwxyz"
upper_case = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
total = 0

DO i = 1 TO input.0
  half = LENGTH(input.i) / 2
  comp_1 = SUBSTR(input.i, 1, half)
  comp_2 = SUBSTR(input.i, half + 1, half)

  common = ""
  DO j = 1 TO half
    letter = SUBSTR(comp_1, j, 1)
    IF POS(letter, comp_2) > 0 THEN DO
      common = letter
      LEAVE
    END
  END

  IF common = "" THEN ITERATE

  IF POS(common, lower_case) > 0 THEN
    priority = POS(common, lower_case)
  ELSE IF POS(common, upper_case) > 0 THEN
    priority = POS(common, upper_case) + 26
  ELSE priority = 0

  total = total + priority
END

SAY "AOC-Y2022-D3P1: Answer:" total
EXIT 0
