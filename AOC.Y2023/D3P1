/* REXX */
dsn_in = "DATA.Y23D3"
"ALLOC DD(DSIN) DATASET('"dsn_in"') SHR"
"EXECIO * DISKR DSIN (FINIS STEM input.)"
"FREE FILE(DSIN)"

sum = 0
nums = "0123456789"
DO r = 1 TO input.0
  line = input.r
  len = LENGTH(line)
  pos_index = 1
  /* Look for digits */
  DO WHILE (pos_index <= len)
    char = SUBSTR(line, pos_index, 1)

    IF POS(char, nums) > 0 THEN DO  /* start of number found */
      start_pos = pos_index

      /* Get subsequent digits */
      num_str = ""
      DO WHILE (pos_index <= len & POS(SUBSTR(line, pos_index, 1), nums) > 0)
        num_str = num_str || SUBSTR(line, pos_index, 1)
        pos_index = pos_index + 1
      END

      is_part = 0
      found = 0
      DO col = start_pos TO pos_index - 1
        DO dr = -1 TO 1
          neighbor_row = r + dr

          IF neighbor_row < 1 | neighbor_row > input.0 THEN ITERATE

          neighbor_line = input.neighbor_row
          neighbor_len = LENGTH(neighbor_line)

          DO dc = -1 TO 1
            IF dr = 0 & dc = 0 THEN ITERATE  /* skip the cell itself */

            neighbor_col = col + dc
            IF neighbor_col < 1 | neighbor_col > neighbor_len THEN ITERATE

            neighbor_char = SUBSTR(neighbor_line, neighbor_col, 1)
            IF (POS(neighbor_char, nums) = 0) & (neighbor_char <> ".") THEN DO
              is_part = 1
              found = 1
              LEAVE
            END

          END
          IF found = 1 THEN LEAVE
        END
        IF found = 1 THEN LEAVE
      END
      IF is_part = 1 THEN DO
        num_val = num_str + 0
        sum = sum + num_val
      END
    END
    ELSE pos_index = pos_index + 1
  END
END

SAY "AOC-Y2023-D3P1: Answer:" sum
EXIT 0
