/* REXX */
dsn_in = "DATA.Y23D4"
"ALLOC DD(DSIN) DATASET('"dsn_in"') SHR"
"EXECIO * DISKR DSIN(FINIS STEM input."
"FREE FILE(DSIN)"

DO i = 1 TO input.0
  card = STRIP(input.i)
  win_nums = STRIP(SUBSTR(card, 1, POS("|", card) - 1))
  my_nums  = STRIP(SUBSTR(card, POS("|", card) + 1))
  match_count = 0

  DO j = 1 TO WORDS(my_nums)
    my_num = WORD(my_nums, j)
    DO k = 1 TO WORDS(win_nums)
      win_num = WORD(win_nums, k)
      IF win_num = my_num THEN DO
        match_count = match_count + 1
        LEAVE
      END
    END
  END

  MATCH.i = match_count
END

DO i = 1 TO input.0
  copies.i = 1
END

DO i = 1 TO input.0
  m = MATCH.i
  IF m > 0 THEN DO
    DO j = i + 1 TO i + m
      IF j > input.0 THEN LEAVE
      copies.j = copies.j + copies.i
    END
  END
END

total = 0
DO i = 1 TO input.0
  total = total + copies.i
END

SAY "AOC-Y23D4P2: Answer:" total
EXIT 0
