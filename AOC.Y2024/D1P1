/* REXX */
dsn_in = "DATA.Y24D1"
"ALLOC DD(DSIN) DATASET('"dsn_in"') SHR"
"EXECIO * DISKR DSIN(FINIS STEM input.)"
"FREE FILE(DSIN)"

DO i = 1 TO input.0
  PARSE VAR input.i val_1 val_2
  list.val1.i = val_1
  list.val2.i = val_2
END

list.val1.0 = input.0
list.val2.0 = input.0
CALL SHELLSORT val1
CALL SHELLSORT val2

total = 0
DO i = 1 TO list.val1.0
  total = total + ABS(list.val1.i - list.val2.i)
END
SAY "AOC-Y2024-D1P1: Answer:" total

EXIT 0

SHELLSORT: PROCEDURE EXPOSE list.
  PARSE ARG val
  N = list.val.0
  I = N % 2

  DO WHILE I <> 0
     DO J = I + 1 TO N
        K = J
        P = K - I
        ZED = list.val.J

        DO WHILE K >= I+1 & list.val.P > ZED
           list.val.K = list.val.P
           K = K - I
           P = K - I
        END

        list.val.K = ZED
     END

     IF I = 2 THEN I = 1
     ELSE I = I * 5 % 11
  END
RETURN
